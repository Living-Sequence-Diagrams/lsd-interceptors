plugins {
    id 'java-library'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'pl.allegro.tech.build.axion-release' version '1.13.2'
}

repositories {
    mavenCentral()
}

group = 'io.github.lsd-consulting'
sourceCompatibility = '11'
project.version = scmVersion.version

ext {
    junitJupiter = '5.6.0'
    junitPlatformVersion = '1.3.2'
}

wrapper {
    gradleVersion = "7.0.2"
}

allprojects {
    plugins.withId('java') {
        dependencies {
            implementation 'io.github.lsd-consulting:lsd-core:0.1.31'
            implementation 'io.github.lsd-consulting:lsd-formatting-library:0.1.8'

            implementation 'org.projectlombok:lombok:1.18.12'
            
            testCompileOnly 'org.projectlombok:lombok:1.18.12'
            testImplementation 'org.assertj:assertj-core:3.15.0'
            testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
            testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiter"
            testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiter"
            testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitJupiter"
            
            annotationProcessor 'org.projectlombok:lombok:1.18.12'
            testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        }
    }
}
subprojects {
    plugins.withId('java-library') {

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        apply plugin: 'maven-publish'
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    groupId = 'io.github.lsd-consulting'
                    artifactId project.name
                    version = "${scmVersion.version}"

                    from components.java
                    artifact sourcesJar
                    artifact javadocJar

                    pom {
                        name = "lsd-interceptors"
                        description = "Provides various interceptors to capture events for the lsd-core library to generate sequence diagrams"
                        url = 'https://github.com/lsd-consulting/lsd-interceptors.git'
                        licenses {
                            license {
                                name = "MIT License"
                                url = "https://github.com/lsd-consulting/lsd-interceptors/blob/main/LICENSE.txt"
                                distribution = "repo"
                            }
                        }
                        developers {
                            developer {
                                name = "Nick"
                                email = "nicholas.mcdowall@gmail.com"
                                organization = 'NKM IT Solutions Ltd'
                                organizationUrl = 'https://github.com/nickmcdowall'
                            }
                            developer {
                                name = "Lukasz"
                                email = "lukasz.gryzbon@gmail.com"
                                organization = 'Integreety Ltd.'
                                organizationUrl = 'https://github.com/integreety'
                            }
                        }
                        scm {
                            url = "https://github.com/lsd-consulting/lsd-interceptors.git"
                        }
                    }
                    repositories {
                        maven {
                            name = 'sonatype'
                            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                            credentials(PasswordCredentials)
                        }
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    sign publishing.publications
}
